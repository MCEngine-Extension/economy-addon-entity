plugins {
    id 'com.gradleup.shadow' version '9.0.2'
    id 'java'
}

version = project.properties[project.name + '.version'] ?: 'undefined'
group = 'io.github.mcengine'

ext {
    // Git credentials for GitHub Packages
    user_git_name = System.getenv('USER_GIT_NAME')
    user_github_token = System.getenv('USER_GITHUB_TOKEN')

    // Final JAR base name (kept as-is for this module)
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEngineEntity'
}

allprojects {
    group = 'io.github.mcengine'

    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }

        ext.configureGitHubRepo = { repoName, repoUrl ->
            maven {
                name = repoName
                url = uri(repoUrl)
                credentials {
                    username = project.user_git_name
                    password = project.user_github_token ?: project.repository_token ?: project.organization_token
                }
            }
        }

        configureGitHubRepo('MCEngine Core API', 'https://maven.pkg.github.com/MCEngine-API/core')
        configureGitHubRepo('MCEngine Economy API', 'https://maven.pkg.github.com/MCEngine-API/economy')

        configureGitHubRepo('MCEngine Economy Common', 'https://maven.pkg.github.com/MCEngine-Common/economy')
        configureGitHubRepo('MCEngine Party Common', 'https://maven.pkg.github.com/MCEngine-Common/party')
    }
}

dependencies {
    // Minecraft Server Platform
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'

    // API
    compileOnly 'io.github.mcengine:core-api:2025.0.9-4'
    compileOnly 'io.github.mcengine:economy-api:2025.0.9-4'

    // Common
    compileOnly 'io.github.mcengine:economy-common:2025.0.9-4'
    compileOnly 'io.github.mcengine:party-common:2025.0.9-4'
}

shadowJar {
    archiveClassifier = null
    archiveBaseName = project.jarName
}

tasks.named('jar').configure {
    enabled = false
}

build.dependsOn(shadowJar)
